plugins {
    id 'war'
    id 'com.google.cloud.tools.jib' version '3.4.2'
}

group 'org.dannyshih.scrabblesolver'
version 'v8.9.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'com.google.guava', name: 'guava', version: '33.2.0-jre'
    implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

task writeVersionFile {
    doLast {
        new File("$buildDir/resources/main/version.txt").write(project.version.toString())
    }
}

war {
    dependsOn writeVersionFile
}

jib {
    to {
        image = 'registry.internal.dannyshih.net:5000/scrabble-solver:' + project.version.toString()
    }

    from.image = 'tomcat:10.1.24-jre21-temurin'
    container {
        appRoot = '/usr/local/tomcat/webapps/ROOT'
        creationTime = '2024-05-17T22:40:00-08:00'
    }

    allowInsecureRegistries = true
}

test {
    useJUnitPlatform()
}