plugins {
    id 'war'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

group 'org.dannyshih.scrabblesolver'
version 'v8.6'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

task writeVersionFile {
    doLast {
        new File("$buildDir/resources/main/version.txt").write(project.version.toString())
    }
}

war {
    dependsOn writeVersionFile
}

jib {
    to {
        image = 'spacechip/scrabble-solver'
        tags = [ project.version.toString() ]
    }

    from.image = 'tomcat:9.0.64-jdk17-temurin'
    container {
        appRoot = '/usr/local/tomcat/webapps/ROOT'
        creationTime = '2022-06-26T17:50:38-07:00'
    }
}

test {
    useJUnitPlatform()
}